*Three stage operational amplifier

*** *** OPAMP SUBCIRCUIT *** ***
.SUBCKT PFC.SUB VP VN VOUT IBIAS VB1 AVDD AVSS

M00 IBIAS IBIAS AVDD AVDD PMOS W=7.7e-06 L=5.39e-06

* differential pair
M10 1     IBIAS AVDD AVDD PMOS W=7.7e-06 L=5.39e-06 M=6

M11 2     VN    1    1    PMOS W=1.564e-05 L=1.67e-06
M12 3     VP    1    1    PMOS W=1.564e-05 L=1.67e-06

* folded cascode
M13 4     IBIAS AVDD AVDD PMOS W=1.962e-05 L=5.39e-06 M=3
M16 5     IBIAS AVDD AVDD PMOS W=1.962e-05 L=5.39e-06 M=3

M14 4     VB1   2    AVSS NMOS W=1.301e-05 L=5.52e-06
M17 5     VB1   3    AVSS NMOS W=1.301e-05 L=5.52e-06

M15 2     4     AVSS AVSS NMOS W=1.133e-05 L=5.34e-06
M18 3     4     AVSS AVSS NMOS W=1.133e-05 L=5.34e-06

* second stage
M20 6     5     AVDD AVDD PMOS W=2.78e-05 L=1.6e-06
M22 7     IBIAS AVDD AVDD PMOS W=3.739e-05 L=5.39e-06

M21 6     6     AVSS AVSS NMOS W=2.36e-05 L=2.34e-06
M23 7     6     AVSS AVSS NMOS W=2.36e-05 L=2.34e-06

* third stage
M30 VOUT  5     AVDD AVDD PMOS W=2.818e-05 L=1.6e-06 M=22
M31 VOUT  7     AVSS AVSS NMOS W=1.289e-05 L=4.11e-06 M=5

* compensation
CM1 5 VOUT 8e-12
CM2 5 7    2e-12
.ENDS PFC.SUB

*** *** SUPPLY VOLTAGES *** ***
VDD VDD 0 3
VSS VSS 0   0

*** *** BIAS VOLTAGE *** ***
VVB1 VB1 VSS DC 1.8

*** *** BIAS CURRENT *** ***
IIBIAS IBIAS VSS 4.1e-06

*** *** SUB-CIRCUIT *** ***
XOPAMP VP VN VOUT IBIAS VB1 VDD VSS PFC.SUB

*** *** LOAD *** ***
RL VOUT VX 25000
CL VOUT VX 1e-10
VX VX VSS '3/2'

*** *** AC LOOP *** ***
VIN VP VSS '3/2' AC 1
RX  VN VOUT 1m AC=1E12
CX  VN VSS 10

*** *** ANALYSIS *** ***
.AC DEC 100 0.001 1E9
.PZ V(VOUT) VIN
.PROBE AC VDB(VOUT)
.PROBE AC VP(VOUT)
.OP
.OPTION POST UNWRAP
.option sim_accuracy=10
.option ingold=2
.option post=2
.option putmeas=0
.INCLUDE p.typ
.INCLUDE n.typ

.MEASURE AC ac_power PARAM='-1*p(VDD)'
.MEASURE AC ZUGF WHEN VDB(VOUT)=0 CROSS=1
.MEASURE AC UGFZSR TRIG AT=1 TARG VDB(VOUT) VAL=0 FALL=1
.MEASURE AC ZSR PARAM='UGFZSR*3.14'
.MEASURE AC dc_gain FIND VDB(VOUT) AT=0.1
.MEASURE AC phaseZPM FIND VP(VOUT) WHEN VDB(VOUT)=0
.MEASURE AC ZPM1 PARAM='180+phaseZPM'
.MEASURE AC ZPM2 PARAM='180+phaseZPM'



.END
